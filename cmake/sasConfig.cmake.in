# - Config file for the sas package
# It defines the following variables
#  sas_SCRIPT_DIR   - Python scripts
#  sas_LIBRARIES    - libraries to link against
#  sas_LIBRARY_DIR  - sas library dir
#  sas_BINARY_DIR   - binary directory
# The macro enable_sas is also defined, allowing the user to disable checkers, and to apply clang-modernize or clang-format

# Compute paths
get_filename_component(sas_CMAKE_DIR "${CMAKE_CURRENT_LIST_FILE}" PATH)

get_filename_component(sas_SCRIPT_DIR "${sas_CMAKE_DIR}/../scripts" ABSOLUTE)
get_filename_component(sas_BINARY_DIR "${sas_CMAKE_DIR}/../bin" ABSOLUTE)
get_filename_component(sas_LIBRARY_DIR "${sas_CMAKE_DIR}/../lib" ABSOLUTE)


FIND_LIBRARY( sas_LIBRARIES NAMES libSas.so PATHS
             ${sas_LIBRARY_DIR}
             NO_DEFAULT_PATH
        )

INCLUDE( FindPackageHandleStandardArgs )
FIND_PACKAGE_HANDLE_STANDARD_ARGS( sas DEFAULT_MSG sas_LIBRARY_DIR sas_SCRIPT_DIR sas_LIBRARIES )

#-----------------------------------------------------------------------------------------------------------------------
# Replaces default cxx compiler with clang and applies coding convention checks. By default, all sas checkers present
# are run unless disabled in the DISABLE_CHECKERS argument. All arguments are optional; calling enable_sas() will run
# checkers without formatting or modernizing.
# Arguments:
# FORMAT: if present, clang-format is called
# MODERNIZE: if present, clang-modernize is called with the options specified in MODERNIZE_OPTIONS
# MODERNIZE_OPTIONS: command-line options to be passed to clang-modernize
# COMPARISON_REPORT_DIR: the directory in which to place the comparison report files; if none is specified
#   ${CMAKE_BINARY_DIR}/sasreport is used.
# DISABLE_CHECKERS: names of checkers to be disabled, separated by ' '.
#-----------------------------------------------------------------------------------------------------------------------
MACRO(enable_sas)
  CMAKE_PARSE_ARGUMENTS (ARG "FORMAT;MODERNIZE"
                        "COMPARISON_REPORT_DIR"
                        "DISABLE_CHECKERS;IGNORE_DIRS;MODERNIZE_OPTIONS" ${ARGN})
  set(CMAKE_CXX_COMPILER ${sas_SCRIPT_DIR}/sas_check)
  message(STATUS "sas enabled.")
  add_definitions(--checkers="sas")
  add_definitions(--source_dir="${CMAKE_SOURCE_DIR}")
  if(ARG_COMPARISON_REPORT_DIR)
    set(sas_REPORT_DIR ${ARG_COMPARISON_REPORT_DIR})
    add_definitions(--comparison_report_dir="${ARG_COMPARISON_REPORT_DIR}")
  else()
    set(sas_REPORT_DIR ${CMAKE_BINARY_DIR}/sasreport)
    add_definitions(--comparison_report_dir="${CMAKE_BINARY_DIR}/sasreport")
  endif(ARG_COMPARISON_REPORT_DIR)

  if(NOT EXISTS ${sas_REPORT_DIR})
    file(MAKE_DIRECTORY ${sas_REPORT_DIR})
  endif()

  file(COPY ${CMAKE_SOURCE_DIR}/ DESTINATION ${sas_REPORT_DIR}/html_diffs FILES_MATCHING REGEX ${CMAKE_BINARY_DIR} EXCLUDE REGEX ".^")

  file(WRITE ${sas_REPORT_DIR}/checker_warnings.txt "")

  if(ARG_MODERNIZE)
    message(STATUS "Clang modernize ON")
    add_definitions(--modernize)
    if(ARG_MODERNIZE_OPTIONS)
      message(STATUS "Using modernize options: ${ARG_MODERNIZE_OPTIONS}")
      add_definitions(--modernize_options="${ARG_MODERNIZE_OPTIONS}")
    endif()
  endif(ARG_MODERNIZE)
  if(ARG_FORMAT)
    set(FORMAT_ON "--check_format")
    message(STATUS "Check-format ON")
    add_definitions(--check_format)
  endif(ARG_FORMAT)
  if(ARG_DISABLE_CHECKERS)
    message(STATUS "Disabled checkers: ${ARG_DISABLE_CHECKERS}")
    add_definitions(--disabled_checkers=${ARG_DISABLE_CHECKERS})
  endif(ARG_DISABLE_CHECKERS)
  set(IGNORE_DIRS ${CMAKE_BINARY_DIR})
  if(ARG_IGNORE_DIRS)
    list(APPEND IGNORE_DIRS ${ARG_IGNORE_DIRS})
  endif(ARG_IGNORE_DIRS)
  add_definitions(--ignore_dirs ${IGNORE_DIRS})
  add_custom_target(report_index ${sas_SCRIPT_DIR}/sas_create_index ${CMAKE_SOURCE_DIR} ${sas_REPORT_DIR} "--ignore_dirs" ${IGNORE_DIRS})

  #Generate html diffs for header files:
  file(GLOB_RECURSE HEADER_FILES ${CMAKE_SOURCE_DIR} "*.h" "*.hpp")
  foreach(HEADER_FILE ${HEADER_FILES})
    string(REPLACE "/" "." NAME ${HEADER_FILE})
    add_custom_target(${NAME} ALL
      COMMAND ${sas_SCRIPT_DIR}/sas_check
                    "${FORMAT_ON}"
                    "--ignore_dirs" ${IGNORE_DIRS}
                    "--do_not_compile"
                    "--comparison_report_dir" "${sas_REPORT_DIR}"
                    "--source_dir" "${CMAKE_SOURCE_DIR}"
                    ${HEADER_FILE})
  endforeach()
ENDMACRO()
